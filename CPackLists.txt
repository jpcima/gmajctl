# -*- cmake -*-

set(CPACK_PACKAGE_VERSION "${PROJECT_VERSION}")
set(CPACK_PACKAGE_VERSION_MAJOR "${PROJECT_VERSION_MAJOR}")
set(CPACK_PACKAGE_VERSION_MINOR "${PROJECT_VERSION_MINOR}")
set(CPACK_PACKAGE_VERSION_PATCH "${PROJECT_VERSION_PATCH}")
set(CPACK_PACKAGE_VENDOR "${PROJECT_VENDOR}")

include(CPack)

if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
  set(windows_bundle_DLLS
    "libgcc_s_sjlj-1.dll" "libgcc_s_seh-1.dll"
    "libstdc++-6.dll" "libwinpthread-1.dll"
    "mgwfltknox-1.3.dll")
  if(ENABLE_GETTEXT)
    list(APPEND windows_bundle_DLLS
      "libintl-8.dll" "iconv.dll")
  endif()
  unset(_dllfile CACHE)
  foreach(dll ${windows_bundle_DLLS})
    find_file(_dllfile "${dll}" PATHS "${FLTK_BIN_DIR}" PATH_SUFFIXES "bin")
    if(_dllfile)
      message("!! DLL \"${dll}\" from \"${_dllfile}\"")
      install(FILES "${_dllfile}" DESTINATION "bin")
    else()
      message("!! DLL \"${dll}\" not found")
    endif()
    unset(_dllfile CACHE)
  endforeach()
endif()

if(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
  set(darwin_bundle_DYLIBS
    "libfltk.1.3.dylib")
  unset(_dylibfile CACHE)
  foreach(dylib ${darwin_bundle_DYLIBS})
    find_library(_dylibfile "${dylib}" PATHS ${FLTK_LIBRARY_DIRS})
    if(_dylibfile)
      message("!! DYLIB \"${dylib}\" from \"${_dylibfile}\"")
      install(FILES "${_dylibfile}" DESTINATION "${PROJECT_NAME}.app/Contents/Frameworks")
      install(CODE "execute_process(COMMAND \"${CMAKE_INSTALL_NAME_TOOL}\" -change \"/opt/local/lib/${dylib}\" \"@rpath/${dylib}\" \"${PROJECT_NAME}.app/Contents/MacOS/${PROJECT_NAME}\")")
    else()
      message("!! DYLIB \"${dylib}\" not found")
    endif()
    unset(_dylibfile CACHE)
  endforeach()
endif()
